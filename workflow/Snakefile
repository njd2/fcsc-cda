from pathlib import Path

src = Path("resources")

res_int = Path("results/intermediate")
res_final = Path("results/final")

res_linkage = res_int / "linkage"
res_meta = res_int / "meta"
res_meta_issues = res_meta / "issues"
res_time_issues = res_int / "time"


rule format_linkage:
    input:
        src / "wg2_linkage.xlsx",
    output:
        table=res_linkage / "wg2_linkage.tsv",
        ex_em=res_linkage / "wg2_ex_em.pdf",
        defined=res_linkage / "wg2_defined_channels.pdf",
    conda:
        "envs/xlsx.yml"
    script:
        "scripts/R/format_linkage.R"


rule read_fcs_meta:
    input:
        src / "fcs_wg2",
    output:
        meta=res_meta / "fcs_meta.tsv.gz",
        params=res_meta / "fcs_params.tsv.gz",
        warnings=res_meta / "warnings.tsv.gz",
    conda:
        "envs/fcsparser.yml"
    script:
        "scripts/python/read_metadata.py"


# rule read_fcs_time_channels:
#     input:
#         channels="static/time_fsc_ssc_mapping.tsv",
#         meta=rules.read_fcs_meta.output.meta,
#     output:
#         "results/intermediate/time_channels.tsv.gz",
#     conda:
#         "envs/fcsparser.yml"
#     threads: 16
#     script:
#         "scripts/python/read_time_channel.py"


rule find_metadata_issues:
    input:
        combos="static/wg2_combinations.tsv",
        tfs="static/time_fsc_ssc_mapping.tsv",
        linkage=rules.format_linkage.output.table,
        meta=rules.read_fcs_meta.output.meta,
        params=rules.read_fcs_meta.output.params,
    output:
        voltgain=res_meta_issues / "voltage_gain.tsv.gz",
        channels=res_meta_issues / "missing_channels.tsv.gz",
        issues=res_meta_issues / "all_issues.tsv.gz",
        clean=res_meta_issues / "clean.tsv.gz",
    params:
        min_event1=2e4,
        min_event2=1.5e4,
        min_event3=5e4,
        vdiff_limit=0.01,
        gdiff_limit=0.01,
    conda:
        "envs/tidyverse.yml"
    script:
        "scripts/R/find_metadata_issues.R"


rule find_time_issues:
    input:
        channels="static/time_fsc_ssc_mapping.tsv",
        meta=rules.find_metadata_issues.output.issues,
    output:
        anomaly=res_time_issues / "anomaly.tsv.gz",
        flat=res_time_issues / "flat.tsv.gz",
        top=res_time_issues / "top.tsv.gz",
        events=res_time_issues / "events.tsv.gz",
    threads: 20
    params:
        min_event1=2e4,
        min_event2=1.5e4,
        min_event3=5e4,
        spike_limit=1000,
        gap_limit=0.05,
        non_mono_limit=100,
        rate_thresh=50,
        min_size=500,
    conda:
        "envs/fcsparser.yml"
    script:
        "scripts/python/find_time_issues.py"


rule summarize_time_issues:
    input:
        meta=rules.read_fcs_meta.output.meta,
        events=rules.find_time_issues.output.events,
        anomaly=rules.find_time_issues.output.anomaly,
    output:
        res_final / "issues_summary.html",
    conda:
        "envs/tidyverse.yml"
    script:
        "scripts/R/summarize_time_issues.Rmd"


# rule run_qc:
#     input:
#         combos="static/wg2_combinations.tsv",
#         event=rules.read_fcs_events.output.summary,
#         meta=rules.read_fcs_meta.output.header,
#         params=rules.read_fcs_meta.output.params,
#         linkage=rules.format_linkage.output.table,
#         insufficient=rules.assess_time_channel.output.insufficient,
#         issues=rules.assess_time_channel.output.indices_issues,
#         nonlinear=rules.assess_time_channel.output.indices_nonlinear,
#     output:
#         errors="results/final/qc/errors.pdf",
#         missing="results/final/qc/missing_channels.tsv",
#     conda:
#         "envs/flowcore.yml"
#     script:
#         "scripts/R/run_qc.R"


rule all:
    input:
        # rules.find_metadata_issues.output,
        rules.find_time_issues.output,
        rules.summarize_time_issues.output,
        # rules.read_fcs_meta.output,
        # rules.format_linkage.output,
        # rules.run_qc.output,
