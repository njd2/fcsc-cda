rule format_linkage:
    input:
        "resources/wg2_linkage.xlsx",
    output:
        table="results/intermediate/linkage/wg2_linkage.tsv",
        ex_em="results/intermediate/linkage/wg2_ex_em.pdf",
        defined="results/intermediate/linkage/wg2_defined_channels.pdf",
    conda:
        "envs/xlsx.yml"
    script:
        "scripts/R/format_linkage.R"


rule read_fcs_meta:
    input:
        "resources/fcs_wg2",
    output:
        header="results/intermediate/meta/fcs_header.tsv.gz",
        params="results/intermediate/meta/fcs_params.tsv.gz",
    conda:
        "envs/flowcore.yml"
    threads: 16
    script:
        "scripts/R/read_fcs_meta.R"


rule read_fcs_events:
    input:
        fcs="resources/fcs_wg2",
        linkage=rules.format_linkage.output.table,
        tfs_mapping="static/time_fsc_ssc_mapping.tsv",
        meta=rules.read_fcs_meta.output.header,
    output:
        summary="results/intermediate/fcs_events_summary.tsv.gz",
        time="results/intermediate/fcs_events_time.tsv.gz",
    conda:
        "envs/flowcore.yml"
    threads: 16
    script:
        "scripts/R/read_fcs_events.R"


rule assess_time_channel:
    input:
        meta=rules.read_fcs_meta.output.header,
        events=rules.read_fcs_events.output.time,
    output:
        valid="results/final/time/valid_gates.tsv.gz",
        insufficient="results/final/time/insufficient_events.tsv.gz",
        indices_nonlinear="results/final/time/indices_nonlinear.tsv.gz",
        indices_issues="results/final/time/indices_with_issues.tsv.gz",
        events_nonlinear="results/final/time/events_nonlinear.tsv.gz",
        events_issues="results/final/time/events_with_issues.tsv.gz",
        pareto="results/final/time/r2_pareto_by_org.pdf",
    params:
        thresh=50,
        min_size=500,
        min_event1=2e4,
        min_event2=1.5e4,
        min_event3=5e4
    conda:
        "envs/tidyverse.yml"
    script:
        "scripts/R/assess_time_channel.R"


rule summarize_time_issues:
    input:
        meta=rules.read_fcs_meta.output.header,
        nonlinear=rules.assess_time_channel.output.events_nonlinear,
        issues=rules.assess_time_channel.output.events_issues,
    output:
        "results/final/time/issues_summary.html",
    conda:
        "envs/tidyverse.yml"
    script:
        "scripts/R/summarize_time_issues.Rmd"
        
        
rule run_qc:
    input:
        combos="static/wg2_combinations.tsv",
        event=rules.read_fcs_events.output.summary,
        meta=rules.read_fcs_meta.output.header,
        params=rules.read_fcs_meta.output.params,
        linkage=rules.format_linkage.output.table,
        insufficient=rules.assess_time_channel.output.insufficient,
        issues=rules.assess_time_channel.output.indices_issues,
        nonlinear=rules.assess_time_channel.output.indices_nonlinear,
    output:
        errors="results/final/qc/errors.pdf",
        missing="results/final/qc/missing_channels.tsv",
    conda:
        "envs/flowcore.yml"
    script:
        "scripts/R/run_qc.R"

rule all:
    input:
        rules.run_qc.output,
